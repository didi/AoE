/*
 * Copyright 2019 The AoE Authors. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * 工程全局变量定义
 */
ext {
    aoe_compile_sdk_version = 29
    aoe_min_sdk_version = 15
    aoe_target_sdk_version = 26

    aoe_version_code = 2
    aoe_version_name = '1.1.1.6'

    kotlin_version = '1.3.61'

    deps = [
            aoe    : [
                    // aoe 核心库统一版本管理
                    library   : [
                            api                : isDebug() ? project(':library-api') : "com.didi.aoe:library-api:$aoe_version_name",
                            logging            : isDebug() ? project(":library-logging") : "com.didi.aoe:library-logging:$aoe_version_name",
                            core               : isDebug() ? project(':library-core') : "com.didi.aoe:library-core:$aoe_version_name",
                            common             : isDebug() ? project(":library-common") : "com.didi.aoe:library-common:$aoe_version_name",
                            service            : isDebug() ? project(':library-service') : "com.didi.aoe:library-service:$aoe_version_name",
                    ],
                    // 第三方推理框架的适配，版本以对应框架版本命名同步，最后加一位小版本号，如TensorFlow 版本2.0.0，AoE 对应的适配版本为2.0.0.x
                    runtime   : [
                            tensorflow: isDebug() ? project(':runtime-tensorflow-lite') : "com.didi.aoe:runtime-tensorflow-lite:2.0.0.1",
                            pytorch   : isDebug() ? project(':runtime-pytorch') : "com.didi.aoe:runtime-pytorch:1.3.1.1",
                            ncnn      : isDebug() ? project(':runtime-ncnn') : "com.didi.aoe:runtime-ncnn:20191113.1",
                            mnn       : isDebug() ? project(':runtime-mnn') : "com.didi.aoe:runtime-mnn:0.2.1.5.1",
                    ],
                    // 拓展插件的版本跟随 aoe 当前版本，不随框架同步更新
                    extensions: [
                            pytorch: isDebug() ? project(':extensions-pytorch') : "com.didi.aoe:extensions-pytorch:1.1.1.5"
                    ]
            ],
            // aoe 计划后续实现全量使用 kotlin，注解依赖暂时使用较低的 26.x 以保证最小的适配成本
            support: [
                    annotation: 'com.android.support:support-annotations:26.1.0'
            ],
            kotlin : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version",
            gson   : 'com.google.code.gson:gson:2.8.5'
    ]
}

/**
 * 内部开发使用工程依赖，降低开发成本
 * @return
 */
private boolean isDebug() {
    return gradle.ext.has("aoeDevEnabled") && gradle.ext.aoeDevEnabled
}

/**
 * 显示所有定义的依赖组件
 */
task showAllDeps() {
    showDepsWithPrefex('', deps)
}

private void showDepsWithPrefex(String prefex, Object parent) {

    if (parent instanceof Map) {
        parent.each {
            if (it.value instanceof Map) {
                showDepsWithPrefex("$prefex${it.key}.", it.value)
            } else {
                println("$prefex${it.key} = ${it.value}")
            }
        }
    }

}
