/*
 * Copyright 2019 The AoE Authors. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * 工程全局变量定义
 */
ext {
    aoe_compile_sdk_version = 29
    aoe_min_sdk_version = 15
    aoe_target_sdk_version = 26

    aoe_version_code = 2
    aoe_version_name = '1.1.1.4'

    kotlin_version = '1.3.61'

    deps = [
            aoe    : [
                    library: [
                            api                : isDebug() ? project(':library-api') : "com.didi.aoe:library-api:$aoe_version_name",
                            logging            : isDebug() ? project(":library-logging") : "com.didi.aoe:library-logging:$aoe_version_name",
                            core               : isDebug() ? project(':library-core') : "com.didi.aoe:library-core:$aoe_version_name",
                            common             : isDebug() ? project(":library-common") : "com.didi.aoe:library-common:$aoe_version_name",
                            service            : isDebug() ? project(':library-service') : "com.didi.aoe:library-service:$aoe_version_name",
                            model_option_loader: isDebug() ? project(':library-model-option-loader') : "com.didi.aoe:library-model-option-loader:$aoe_version_name"
                    ],
                    runtime: [
                            tensorflow: isDebug() ? project(':runtime-tensorflow-lite') : "com.didi.aoe:runtime-tensorflow-lite:2.0.0.0",
                            pytorch: isDebug() ? project(':runtime-pytorch') : "com.didi.aoe:runtime-pytorch:1.3.1.0",
                            ncnn: isDebug() ? project(':runtime-ncnn') : "com.didi.aoe:runtime-ncnn:$aoe_version_name",
                            mnn: isDebug() ? project(':runtime-mnn') : "com.didi.aoe:runtime-mnn:0.2.1.5.0",
                    ]
            ],
            support: [
                    annotation: 'com.android.support:support-annotations:26.1.0'
            ],
            kotlin : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version",
            gson   : 'com.google.code.gson:gson:2.8.5'
    ]
}

private boolean isDebug() {
    return gradle.ext.has("aoeDevEnabled") && gradle.ext.aoeDevEnabled
}

/**
 * 显示所有定义的全局依赖组件
 */
task showAllDeps() {
    showDepsWithPrefex('', deps)
}

private void showDepsWithPrefex(String prefex, Object parent) {

    if (parent instanceof Map) {
        parent.each {
            if (it.value instanceof Map) {
                showDepsWithPrefex("$prefex${it.key}.", it.value)
            } else {
                println("$prefex${it.key} = ${it.value}")
            }
        }
    }

}
