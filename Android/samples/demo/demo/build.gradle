apply plugin: 'com.android.application'
apply from: '../../../constants.gradle'

android {
    compileSdkVersion project.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.didi.aoe.examples.demo"
        minSdkVersion project.ext.minAppSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode project.ext.releaseVersionCode
        versionName project.ext.releaseVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        // The demo app isn't indexed, doesn't have translations, and has a
        // banner for AndroidTV that's only in xhdpi density.
        disable 'GoogleAppIndexingWarning', 'MissingTranslation', 'IconDensities'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation 'android.arch.navigation:navigation-fragment:1.0.0'

    def app_support = '28.0.0'
    implementation "com.android.support:appcompat-v7:$app_support"
    implementation "com.android.support:recyclerview-v7:$app_support"
    implementation "com.android.support:cardview-v7:$app_support"
    implementation "com.android.support:design:$app_support"
    implementation "com.android.support.constraint:constraint-layout:1.1.3"

    if (project.ext.has("aoePublishEnabled") && project.ext.aoePublishEnabled) {
        implementation "com.didi.aoe:library-core:$project.ext.releaseVersion"
        implementation "com.didi.aoe:features-mnist:$project.ext.releaseVersion"
        implementation "com.didi.aoe:features-squeeze:$project.ext.releaseVersion"
    } else {
        implementation project(':library-core')
        implementation project(':features-mnist')
        implementation project(':features-squeeze')
    }

}
