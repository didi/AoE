apply plugin: 'com.android.application'
apply from: '../../constants.gradle'

apply from: "../tools/download-models.gradle"

android {
    compileSdkVersion project.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.didi.aoe.examples.demo"
        minSdkVersion project.ext.minAppSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode project.ext.releaseVersionCode
        versionName project.ext.releaseVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        // The demo app isn't indexed, doesn't have translations, and has a
        // banner for AndroidTV that's only in xhdpi density.
        disable 'GoogleAppIndexingWarning', 'MissingTranslation', 'IconDensities'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        pickFirst 'lib/*/libc++_shared.so'
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'androidx.navigation:navigation-fragment:2.2.0-alpha03'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    if (project.ext.has("aoePublishEnabled") && project.ext.aoePublishEnabled) {
        implementation "com.didi.aoe:library-core:$project.ext.releaseVersion"
        implementation "com.didi.aoe:features-mnist:$project.ext.releaseVersion"
        implementation "com.didi.aoe:features-squeeze:$project.ext.releaseVersion"
        implementation "com.didi.aoe:features-recognize:$project.ext.releaseVersion"
    } else {
        implementation project(':library-core')
        implementation project(':features-mnist')
        implementation project(':features-squeeze')
        implementation project(':features-recognize')
    }

}
